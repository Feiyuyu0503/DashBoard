plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
}

def gitCommitCount = "git rev-list HEAD --count".execute().text.trim()
def gitCommitHash = "git rev-parse --verify --short HEAD".execute().text.trim()

android {
    compileSdkVersion 33
    buildToolsVersion "30.0.3"
    ndkVersion "22.1.7171670"

    defaultConfig {
        applicationId "com.dashboard.kotlin"
        minSdkVersion 26
        targetSdkVersion 33
        versionCode gitCommitCount.toInteger()
        versionName "5.5"
        versionNameSuffix = ".r${gitCommitCount}.${gitCommitHash}"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild{
            cmake{
                cppFlags "-std=c++20"
                abiFilters 'arm64-v8a'
            }
        }
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "$rootProject.name-v${variant.versionName}-${variant.buildType.name}.apk"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
            //proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),'proguard-rules.pro'
        }
        debug {}
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    externalNativeBuild {
        cmake {
            version = "3.18.1"
            path file('./src/cpp/CMakeLists.txt')
        }
    }
    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.3'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.3'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.google.android.material:material:1.8.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //mmkv
    implementation 'com.tencent:mmkv-static:1.2.14'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.10'
    implementation 'com.google.code.gson:gson:2.10.1'

    def libsuVersion = '4.0.3'
    implementation "com.github.topjohnwu.libsu:core:${libsuVersion}"
    implementation "com.github.topjohnwu.libsu:busybox:${libsuVersion}"
}